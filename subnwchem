#!/bin/csh

# Interactive PBS submission for running NWChem job with 
# MPI parallel on Taiwania cluster, NCHC, Taiwan
#
# Capabilities:
#  [/] works with serial, MPI, & OpenMP parallel methods
#  [/] submit job on GPU/CUDA request
#  [/] automatically determine optimal PBS job queue
#  [/] able to use ARMCI methods: Casper and MPI-PR
#
# Updated 2018.06.20  Rangsiman Ketkaew  rangsiman1993@gmail.com
######################################################################
set NWCHEM_VER = "6.8.1"
set NWCHEM_CASPER = "/pkg/nwchem/Casper/i18gcc6/nwchem-6.8.1-fixmrcc"
set NWCHEM_CASPER_GPU = "/pkg/nwchem/Casper/i18/nwchem-6.8.1-fixcuda"
set NWCHEM_MPIPR = "/pkg/nwchem/MPI-PR/i18gcc6/nwchem-6.8.1-fixmrcc"
set NWCHEM_MPIPR_GPU = "NOTAVAIALBLE"
set NWCHEM_GPU = "$NWCHEM_CASPER_GPU"
set CLEANSRC = "/pkg/chem/sys/bin/"
######################################################################

onintr int

echo ""
echo "       #############################################"
echo "      ##  NWChem 6.8 Interactive PBS submission  ##"
echo "     ##           on Taiwania Cluster           ##"
echo "    #############################################"
echo ""

#############################################
# Check each argument.
#############################################

set GPU = 0
set USEGPU = 0
set ARMCI = 0
set CASPER = 0
set CASPER_GPU = 0
set MPIPR = 0
set MPIPR_GPU = 0
set ARGV_ORDER = "$#argv"

if ($ARGV_ORDER != 0) then
  @ i = 1
  while ($i <= $ARGV_ORDER)
    set ARGV = $argv[$i]
#
    if ("null$ARGV" == "nullgpu") then
      set USEGPU = 1
    else if ("null$ARGV" == "nullcasper") then
      set ARMCI = 1
      set CASPER = 1
    else if ("null$ARGV" == "nullmpipr") then
      set ARMCI = 1
      set MPIPR = 1
    else if ("null$ARGV" == "nullhelp") then
      clear
      goto help
    else 
      echo "Error: invalid argument $ARGV"
      exit 1
    endif
#
    @ i ++
  end
endif

#############################################
# Check if GPU and/or ARMCI are requested.
#############################################

if ($USEGPU == 1) then
  echo "Enabled NWChem/CUDA"
  if ($ARMCI == 1) then
    goto armci_check
  else
# ARMCU is not used
    set NWCHEM_TOP = "$NWCHEM_GPU"
# to define $NWCHEM_RUNTIME
    set GPU = 1
    goto start_program
  endif
else
  if ($ARMCI == 1) then
    goto armci_check
  else
    set CASPER = 0
    set MPIPR = 0
    goto nwchem_def
  endif
endif 

#############################################
# If ARMCI is requested, check either Casper
# of MPI-PR will be used.
#############################################

armci_check:
if ( $CASPER == $MPIPR ) then
  echo "Error: Conflict in ARM-CI. Use either Casper or MPI-PR."
  exit 1
endif

if ("null$CASPER" == "null1") then
  set NWCHEM_TOP = "$NWCHEM_CASPER"
  set CASPER = 1
  set MPIPR = 0
  echo "Enabled ARMCI Casper"
  if ($USEGPU == 1) then
    set NWCHEM_TOP = "$NWCHEM_CASPER_GPU"
# to define $NWCHEM_RUNTIME
    set CASPER_GPU = 1
    goto start_program
  endif
  goto start_program
#
else if ("null$MPIPR" == "null1") then
  set NWCHEM_TOP = "$NWCHEM_MPIPR"
  set CASPER = 0
  set MPIPR = 1
  echo "Enabled ARMCI MPI-PR"
  if ($USEGPU == 1) then
# Turn-off NWChem GPU/MPI-PR
    echo "Error: NWChem with GPU & MPI-PR is not available."
#
    set NWCHEM_TOP = "$NWCHEM_MPIPR_GPU"
# to define $NWCHEM_RUNTIME
    set MPIPR_GPU = 1
    goto start_program
  endif
  goto start_program
endif

#############################################
# If neither GPU nor ARMCI are requested,
# use NWChem Casper. Ghost processor is not
# used even if we chose Casper is used.
#############################################

nwchem_def:
set NWCHEM_TOP = "$NWCHEM_CASPER"
set CASPER = 0
set MPIPR = 0


#############################################
# Start program: user is asked to fill the
# necessary information for creating PBS job.
#############################################

start_program:

#if (! -e $NWCHEM_TOP) then
#  echo 'Error: Unable to locate $NWCHEM_TOP'
#  exit 1
#endif

#############################################
# Determine Input file.
#############################################

  pushd $HOME >& /dev/null
  set PWDHOME = `pwd`
  popd >& /dev/null
  set FULLPATH = `pwd | sed -e "s,$PWDHOME,~,"`

#  echo "" 
  search_input:
  set DEFAULTINPUT = "$FULLPATH/nwchem.nw"
  echo -n "Enter input file [$DEFAULTINPUT]: "
  set JOBINPUT = "$<"
  if ("null$JOBINPUT" == "null") set JOBINPUT = "$DEFAULTINPUT"
#
  set INPUTFILE = "$FULLPATH/$JOBINPUT"
  if (-f $INPUTFILE) then
     set JOBINPUT = "$INPUTFILE"
   else if (-f $INPUTFILE.nw) then
     set JOBINPUT = "$INPUTFILE.nw"
  endif
#
  if (! -f $JOBINPUT) then
    echo "Error: failed to locate input file "$JOBINPUT""
    goto search_input
  endif

#############################################
# Define name of output file.
#############################################

  set JOBOUTPUT = `basename $JOBINPUT .nw`.out
  @ i = 1
  while (-e $JOBOUTPUT) 
    echo " $JOBOUTPUT already exists !"
    set JOBOUTPUT = `basename $JOBINPUT .nw`.$i.out
    @ i ++
  end
  set DEFAULTOUTPUT = "$FULLPATH/$JOBOUTPUT"
#
  echo -n "Enter output file [$DEFAULTOUTPUT]: "
  set OUTPUTFILE = "$<"
  if ("null$OUTPUTFILE" != "null") then
   set JOBOUTPUT = "$FULLPATH/$OUTPUTFILE".out
  else
   set JOBOUTPUT = "$DEFAULTOUTPUT"
  endif

#############################################
# Define Computing Resource for GPU queue.
#############################################

ask_gpu:
if ($USEGPU == 1) then
  echo -n "Number of GPU cores [1]: "
  set JOBGPU = "$<"
  if ("null$JOBGPU" == "null") then
   set JOBGPU = 1
  else if ($JOBGPU >= 33) then
   echo "Error: Maximum GPU is 32"
   goto ask_gpu
  else if ($JOBGPU <= 0) then
   echo "Error: Enter only positive integer"
   goto ask_gpu
  endif
endif

# If GPU/CUDA is enabled, skip CPU queue
if ($USEGPU == 1) then
  set TOTALGPU = "$JOBGPU"
  goto gpu_queue
endif

#############################################
# Define Computing Resource for CPU queue.
#############################################

  ask_node:
  echo -n "Number of Compute node [1]: "
  set JOBNODE = "$<"
  if ("null$JOBNODE" == "null") then
   set JOBNODE = 1
  else if ($JOBNODE >= 601) then
   echo "Error: Maximum node is 600"
   goto ask_node
  else if ($JOBNODE <= 0) then
   echo "Error: Enter only positive integer"
   goto ask_node
  endif
#
  ask_cpu:
  echo -n "Number of CPU cores [40]: "
  set JOBCPU = "$<"
  if ("null$JOBCPU" == "null") then
   set JOBCPU = 40
  else if ($JOBCPU >= 41) then
   echo "Error: Maximum CPU cores/node is 40"
   goto ask_cpu
  else if ($JOBCPU <= 0) then
   echo "Error: Enter only positive integer"
   goto ask_cpu
  endif

#############################################
# Number of MPI process is set to = $JOPCPU
# and OMP Threads is set to 1 per MPI.
# Uncomment following lines to unlock the 
# default setting.
#############################################

  #ask_mpi:
  #echo -n "Number of MPI process [$JOBCPU]: "
  #set JOBMPI = "$<"
  #if ("null$JOBMPI" == "null") then
   set JOBMPI = $JOBCPU
  #else if ($JOBMPI >= 41) then
  # echo "Error: Maximum MPI process/node is 40"
  # goto ask_mpi
  #else if ($JOBMPI <= 0) then
  # echo "Error: Enter only positive integer"
  # goto ask_mpi
  #endif
#
  #ask_omp:
  #echo -n "Number of OMP Threads [1]: "
  #set JOBOMP = "$<"
  #if ("null$JOBOMP" == "null") then
   set JOBOMP = 1
  #else if ($JOBOMP >= 41) then
  # echo "Error: Maximum OpenMP Threads/node is 40"
  # goto ask_omp
  #else if ($JOBOMP <= 0) then
  # echo "Error: Enter only positive integer"
  # goto ask_omp
  #endif

set TOTALGPU = "-"

#############################################
# Calculate total number of MPI process.
#############################################

@ TOTALMPI = ($JOBNODE * $JOBMPI)
if ( $CASPER != 0 || $MPIPR != 0 ) then
 if ($TOTALMPI == 1) then
  echo "Error: Casper or MPI-PR requires MPI process > 1"
  goto ask_node
 endif
endif

#############################################
# Search optimal CPU queue and wall-time.
#############################################

cpu_queue:
  if ($TOTALMPI == 1) then
   set CPUQUEUE = "serial"
   set JOBTIME = "96:00:00"
  else if ($TOTALMPI <= 40) then
   set CPUQUEUE = "cf40"
   set JOBTIME = "96:00:00"
  else if ($TOTALMPI <= 160) then
   set CPUQUEUE = "cf160"
   set JOBTIME = "96:00:00"
  else if ($TOTALMPI <= 400) then
   set CPUQUEUE = "ct400"
   set JOBTIME = "96:00:00"
  else if ($TOTALMPI <= 800) then
   set CPUQUEUE = "ct800"
   set JOBTIME = "72:00:00"
  else if ($TOTALMPI <= 1200) then
   set CPUQUEUE = "cf1200"
   set JOBTIME = "48:00:00"
  else if ($TOTALMPI <= 2000) then
   set CPUQUEUE = "ct2k"
   set JOBTIME = "48:00:00"
  else if ($TOTALMPI <= 6000) then
   set CPUQUEUE = "ct6k"
   set JOBTIME = "24:00:00"
  else if ($TOTALMPI <= 8000) then
   set CPUQUEUE = "ct8k"
   set JOBTIME = "24:00:00"
  else if ($TOTALMPI <= 22400) then
   set CPUQUEUE = "ct22400"
   set JOBTIME = "24:00:00"
  endif

  echo -n "Job Queue [optimal queue is $CPUQUEUE]: "
  set CHECKQUEUE = "$<"
  if ("null$CHECKQUEUE" == "null") then
    set JOBQUEUE = "$CPUQUEUE"
   else if ("null$CHECKQUEUE" == "nullctest") then
    set JOBQUEUE = ctest
    set JOBTIME = "00:30:00"
   else
    set JOBQUEUE = "$CHECKQUEUE"
    echo -n "Set wall-time [e.g. 00:30:00]: "
    set JOBTIME = "$<"
  endif

# If CPU queue is used, disabled GPU
  set JOBGPU = "-" 
  set SETGPU = ""
# Skip GPU queue
  goto job_setting

#############################################
# Search optimal GPU queue and wall-time.
# Previously, $TOTALGPU is set to $JOBCPU.
#############################################

gpu_queue:
  if ($TOTALGPU <= 4) then
   set GPUQUEUE = "gp4"
   set JOBTIME = "96:00:00"
  else if ($TOTALGPU <= 16) then
   set GPUQUEUE = "gp16"
   set JOBTIME = "96:00:00"
  else if ($TOTALGPU <= 32) then
   set GPUQUEUE = "gp32"
   set JOBTIME = "48:00:00"
  endif

#############################################
# Number of Compute node that user can 
# request depends on GPU job queue.
#############################################

# gp4
  if ($GPUQUEUE == gp4) then
    set JOBNODE = 1
    echo "Number of Compute node for this queue is only 1"
# gp16
  else if ($GPUQUEUE == gp16) then
    ask_node_for_gpu:
    echo -n "Number of Compute node [2]: "
    set JOBNODE = "$<"
    if ("null$JOBNODE" == "null") then
     set JOBNODE = 1
    else if ($JOBNODE >= 5) then
     echo "Error: Maximum node is 4"
     goto ask_node_for_gpu
    else if ($JOBNODE <= 0) then
     echo "Error: Enter only positive integer"
     goto ask_node_for_gpu
    else if ($JOBNODE <= 1) then
     echo "Error: Minimum node is 2"
     goto ask_node_for_gpu
    endif
# gp32
  else if ($GPUQUEUE == gp32) then
    ask_node_for_gpu:
    echo -n "Number of Compute node [2]: "
    set JOBNODE = "$<"
    if ("null$JOBNODE" == "null") then
     set JOBNODE = 1
    else if ($JOBNODE >= 9) then
     echo "Error: Maximum node is 8"
     goto ask_node_for_gpu
    else if ($JOBNODE <= 0) then
     echo "Error: Enter only positive integer"
     goto ask_node_for_gpu
    else if ($JOBNODE <= 4) then
     echo "Error: Minimum node is 5"
     goto ask_node_for_gpu
    endif
  endif

  ask_cpu_for_gpu:
  echo -n "Number of CPU cores [40]: "
  set JOBCPU = "$<"
  if ("null$JOBCPU" == "null") then
   set JOBCPU = 40
  else if ($JOBCPU >= 41) then
   echo "Error: Maximum CPU cores/node is 40"
   goto ask_cpu_for_gpu
  else if ($JOBCPU <= 0) then
   echo "Error: Enter only positive integer"
   goto ask_cpu_for_gpu
  endif
# 
  set JOBMPI = $JOBCPU
#
  set JOBOMP = 1
#

  echo -n "Job Queue [optimal GPU queue is $GPUQUEUE]: "
  set CHECKQUEUE = "$<"
  if ("null$CHECKQUEUE" == "null") then
    set JOBQUEUE = "$GPUQUEUE"
  else if ("null$CHECKQUEUE" == "nullgtest") then
# 
    ask_gpu_for_gtest:
    if ($USEGPU == 1) then
      echo -n "Number of GPU cores [1]: "
      set JOBGPU = "$<"
      if ("null$JOBGPU" == "null") then
        set JOBGPU = 1
      else if ($JOBGPU >= 9) then
        echo "Error: Maximum GPU is 8"
        goto ask_gpu
      else if ($JOBGPU <= 0) then
        echo "Error: Enter only positive integer"
        goto ask_gpu
      endif
    endif
#
     set TOTALGPU = $JOBGPU
# determine optimal number of cpu
    ask_cpu_for_gtest:
    echo -n "Number of CPU cores [4]: "
    set JOBCPU = "$<"
    if ("null$JOBCPU" == "null") then
      set JOBCPU = 4
    else if ($JOBCPU >= 9) then
      echo "Error: Maximum CPU cores/node is 8"
      goto ask_cpu_for_gtest
    else if ($JOBCPU <= 0) then
      echo "Error: Enter only positive integer"
      goto ask_cpu_for_gtest
    endif
# 
    set JOBQUEUE = gtest
    set JOBTIME = "00:30:00"
    set JOBNODE = 1
    set JOBMPI = $JOBCPU
    set JOBOMP = 1
  else
    set JOBQUEUE = "$CHECKQUEUE"
    echo -n "Set wall-time [e.g. 00:30:00]: "
    set JOBTIME = "$<"
endif

# Define 'ngpus' for PBS script
  set SETGPU = ":ngpus=$TOTALGPU"

#############################################
# Calculate total number of MPI process.
#############################################

@ TOTALMPI = ($JOBNODE * $JOBMPI)
#
if ( $CASPER != 0 || $MPIPR != 0 ) then
 if ($TOTALMPI == 1) then
  echo "Error: Casper or MPI-PR requires MPI process > 1"
  goto ask_node
 endif
endif 

# Determining computing resources is done

#############################################
# Define job name & standard error & output 
# and check project ID.
#############################################

job_setting:
set JOBNAME = `basename $JOBINPUT .nw`
set JOBSTDERR = `basename $JOBINPUT .nw`.stderr
set JOBSTDOUT = `basename $JOBINPUT .nw`.stdout
set INPUTNAME = `basename $JOBINPUT .nw`.nw
set OUTPUTNAME = `basename $JOBOUTPUT .out`.out
if (-f /usr/bin/get_su_balance) then
 set PROJ_ID = `/usr/bin/get_su_balance |awk -F, '{print $2}'`
else
 set PROJ_ID = '$PROJ_ID'
endif

#############################################
# Set NWChem runtime and determine clean-up 
# file for Casper and MPI-PR methods.
#############################################

if ("null$GPU" == "null1") then
  set NWCHEM_RUNTIME = GPU
  set NWCHEM_TYPE = "GPU"
else if ("null$CASPER_GPU" == "null1") then
  set NWCHEM_RUNTIME = CASPER_GPU
  set NWCHEM_TYPE = "ARMCI: Casper & GPU"
else if ("null$MPIPR_GPU" == "null1") then
  set NWCHEM_RUNTIME = MPIPR_GPU
  set NWCHEM_TYPE = "ARMCI: MPI-PR & GPU"
else if ("null$CASPER" == "null1") then
  set NWCHEM_RUNTIME = CASPER
  set NWCHEM_TYPE = "ARMCI: Casper"
else if ("null$MPIPR" == "null1") then
  set NWCHEM_RUNTIME = MPIPR
  set NWCHEM_TYPE = "ARMCI: MPI-PR"
else
  set NWCHEM_RUNTIME = NORM
  set NWCHEM_TYPE = "No ARMCI"
endif

#############################################
# determine clean-up file for Casper and
# MPI-PR
#############################################

if ($NWCHEM_RUNTIME == GPU) then
  set CLEAN_CASPER = 1
else if ($NWCHEM_RUNTIME == CASPER_GPU || $NWCHEM_RUNTIME == CASPER) then
  set CLEAN_CASPER = 1
else if ($NWCHEM_RUNTIME == MPIPR_GPU || $NWCHEM_RUNTIME == MPIPR) then
  set CLEAN_MPIPR = 1
else if ($NWCHEM_RUNTIME == NORM) then
  set CLEAN_CASPER = 1
endif

set CLEAN_MPIPR = 0

if ($CLEAN_CASPER == 1) then
  set CLEANCASPER = "$CLEANSRC/cleanup-devshm.sh"
  if (! -f $CLEANCASPER) echo "Error: failed to locate cleanup-devshm.sh in $CLEANSRC"
  if (! -f $HOME/.cleanup-devshm.sh) cp $CLEANCASPER $HOME/.cleanup-devshm.sh
else if ($CLEAN_MPIPR == 1) then
  set CLEAN_MPIPR = "$CLEANSRC/cleanup-cmx.sh"
  if (! -f $CLEANMPIPR) echo "Error: failed to locate cleanup-cmx.sh in $CLEANSRC"
  if (! -f $HOME/.cleanup-cmx.sh) cp $CLEANMPIPR $HOME/.cleanup-cmx.sh
endif

#############################################
# Show all parameters for job submission 
# using user-defined environment.
#############################################

  echo "\n"
  echo " ========== Necessary Settings for PBS submission are following ========== "
  echo " Run on `date` by `whoami`"
  echo ""
  echo " NWChem runtime         = $NWCHEM_VER - $NWCHEM_TYPE"
  echo " NWCHEM_EXE             = $NWCHEM_TOP/bin/LINUX64/nwchem"
  echo ""
  echo " Input file             = $JOBINPUT"
  echo " Output file            = $JOBOUTPUT"
  echo ""
  echo " Number of compute node = $JOBNODE"
  echo " Number of CPU cores    = $JOBCPU"
  echo " Number of GPU          = $TOTALGPU"
  echo " Number of MPI process  = $JOBMPI"
  echo " Number of OMP Threads  = $JOBOMP"
  echo " Total MPI process      = $JOBNODE x $JOBMPI = $TOTALMPI processes"
  echo ""
  echo " Job Name               = $JOBNAME"
  echo " Job Queue              = $JOBQUEUE"
  echo " Wall-time              = $JOBTIME"
  echo " Std Error              = $FULLPATH/$JOBSTDERR"
  echo " Std Output             = $FULLPATH/$JOBSTDOUT"
  echo " Project ID             = $PROJ_ID"
  echo " ==========================================================================\n"

  set SETNODE    = "select=$JOBNODE"
  set SETNCPUS   = ":ncpus=$JOBCPU"
  set SETMPIPROCS = ":mpiprocs=$JOBMPI"
  set SETTHREADS = ":ompthreads=$JOBOMP"
# SETGPU env was already defined above.

echo -n "Submit your job now ? [yes]: "
set CONFIRM = "$<"
if ("null$CONFIRM" == "null" || "null$CONFIRM" == "nully" || "null$CONFIRM" == "nullyes") then
else
  echo "Error: PBS submission aborted !"
  exit 1
endif

#############################################
# Now creat PBS script and write all 
# configuration settings.
#############################################

set JOB_SCRIPT = `dirname $JOBOUTPUT`/submit.`basename $JOBOUTPUT .out`.sh

cat <<EOF > $JOB_SCRIPT
#!/bin/bash
#PBS -l $SETNODE$SETNCPUS$SETMPIPROCS$SETTHREADS$SETGPU
#PBS -l walltime=$JOBTIME
#PBS -q $JOBQUEUE
#PBS -N $JOBNAME
#PBS -e $JOBSTDERR
#PBS -o $JOBSTDOUT
#PBS -P $PROJ_ID

module load intel/2018_u1 cuda/8.0.61 gcc/6.3.0

cd \$PBS_O_WORKDIR

ulimit -c 0
ulimit -s unlimited

export SCRATCH_DIR=/work1/$USER/SCRATCH/nwchem/nwchem.pbs\${PBS_JOBID/\.srvc1/}

if [ ! -d \$SCRATCH_DIR ]; then 
  mkdir -p \$SCRATCH_DIR
fi

export JOBPATH=$FULLPATH
export I_MPI_PIN_DOMAIN=omp
export MPI_ROOT=\$I_MPI_ROOT/intel64
export MPICC=\$MPI_ROOT/bin/mpiicc
export MPICXX=\$MPI_ROOT/bin/mpiicpc
export MPIFC=\$MPI_ROOT/bin/mpiifort
export NWCHEM_TOP=$NWCHEM_TOP
export NWCHEM_TARGET=LINUX64
export NWCHEM_EXE=\$NWCHEM_TOP/bin/\$NWCHEM_TARGET/nwchem
export NWCHEM_CASLIB=\$NWCHEM_TOP/../deps/lib/
export NWCHEM_BASIS_LIBRARY=/pkg/nwchem/nwchem_basis/basis/libraries/
export NWCHEM_NWPW_LIBRARY=/pkg/nwchem/nwchem_nwpw/nwpw/libraryps/

if [ ! -f ~/.nwchemrc ]; then 
 ln -s /pkg/nwchem/etc/default.nwchemrc ~/.nwchemrc
fi

export MACHLIST=\$PBS_O_WORKDIR/nodelist.\${PBS_JOBID/\.srvc1/}

cat \$PBS_NODEFILE | sed -e 's/.*/&\.nchc\.opa/' > \$MACHLIST
export CASCLEAN="for RUNNODE in \`uniq \$MACHLIST\`; ssh \$RUNNODE \$HOME/.cleanup-devshm.sh; done"
export MPICLEAN="for RUNNODE in \`uniq \$MACHLIST\`; ssh \$RUNNODE \$HOME/.cleanup-cmx.sh; done"

EOF

#############################################
# append optimal clean command and 
# MPI command to script.
#############################################

if ("null$NWCHEM_RUNTIME" == "nullGPU") then
cat <<EOF >> $JOB_SCRIPT
\$CASCLEAN
mpirun -PSM2 -n $TOTALMPI \$NWCHEM_EXE \
\$JOBPATH/$INPUTNAME > \$JOBPATH/$OUTPUTNAME
\$CASCLEAN

EOF
#
else if ("null$NWCHEM_RUNTIME" == "nullCASPER_GPU" || "null$NWCHEM_RUNTIME" == "nullCASPER") then
cat <<EOF >> $JOB_SCRIPT
export ARMCI_NETWORK=ARMCI
\$CASCLEAN
mpirun -PSM2 -n $TOTALMPI -genv CSP_NG 1 -genv LD_PRELOAD \$NWCHEM_CASLIB/libcasper.so \
\$NWCHEM_EXE \$JOBPATH/$INPUTNAME > \$JOBPATH/$OUTPUTNAME
\$CASCLEAN

EOF
#
else if ("null$NWCHEM_RUNTIME" == "nullMPIPR_GPU" || "null$NWCHEM_RUNTIME" == "nullMPIPR") then
cat <<EOF >> $JOB_SCRIPT
export ARMCI_NETWORK=MPI-PR
\$MPICLEAN
mpirun -PSM2 -n $TOTALMPI \$NWCHEM_EXE \$JOBPATH/$INPUTNAME > \$JOBPATH/$OUTPUTNAME
\$MPICLEAN

EOF
#
else if ("null$NWCHEM_RUNTIME" == "nullNORM") then
cat <<EOF >> $JOB_SCRIPT
\$CASCLEAN
mpirun -PSM2 -n $TOTALMPI \$NWCHEM_EXE \$JOBPATH/$INPUTNAME > \$JOBPATH/$OUTPUTNAME
\$CASCLEAN

EOF

endif

#############################################
# Now submit the job using 'qsub'.
#############################################

qsub $JOB_SCRIPT
 echo "Your job is submitted."
 exit 0

int:  
 echo "\nError: You pressed Ctrl-C  ....quit.... \n"
 exit 1

help:
 echo ""
 echo " NAME                    subnwchem\n"
 echo " SYNOPSIS                subnwchem [gpu||casper||mpipr] [info] [help]\n"
 echo " EXAMPLE                 subnwchem gpu             submit NWChem using CUDA"
 echo "                         subnwchem gpu casper      submit NWChem using CUDA and Casper\n"
 echo " DESCRIPTION             Interactive PBS Professional Job Submission for NWChem $NWCHEM_VER."
 echo "                         IT can be used to submit NWChem job with and without using ARMCI"
 echo "                         methods, Casper and MPI-PR, and with and without GPU/CUDA."
 echo "                         Note that GPU/MPI-PR is not available.\n"   
 echo " COMMANDS"
 echo "   gpu                   Requests the use of GPU accelerator."
 echo "   casper                Requests Casper method (against MPI-PR)."
 echo "   mpipr                 Requests MPI-PR method (against Casper)."
 echo "   help                  Open this help.\n"
 echo " LIMITATION"
 echo "   Maximum node          600"
 echo "   Maximum CPU cores     40 (per node)"
 echo "   Maximum MPI process   40 (per node)"
 echo "   Maximum Threads       40 (per node & per MPI process)\n"
 echo " CONTACT                 Rangsiman Ketkaew  rangsiman1993@gmail.com\n"
 echo " MORE DETAILS            NWChem Official Website  http://www.nwchem-sw.org"
 echo "                         NWChem Official manual   https://github.com/nwchemgit/nwchem/wiki\n"
 exit 0
